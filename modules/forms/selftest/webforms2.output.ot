/* -*- Mode: c++; tab-width: 4; indent-tabs-mode: t; c-basic-offset: 4 -*- */
group "WebForms2.Output";
require init;

include "modules/logdoc/htm_elm.h";
include "modules/prefs/prefsmanager/collections/pc_js.h";

language ecmascript;

global
{
#ifdef DELAYED_SCRIPT_EXECUTION
	int dse_pref;
#endif // DELAYED_SCRIPT_EXECUTION
}

setup
{
#ifdef DELAYED_SCRIPT_EXECUTION
	// "WebForms2.Output.InitialDefaultValue" fails is DSE is enabled
	// because the <output> has a <script> element inside.
	dse_pref = g_pcjs->GetIntegerPref(PrefsCollectionJS::DelayedScriptExecution);
	if (dse_pref)
		g_pcjs->WriteIntegerL(PrefsCollectionJS::DelayedScriptExecution, 0);
#endif // DELAYED_SCRIPT_EXECUTION
}

exit
{
#ifdef DELAYED_SCRIPT_EXECUTION
	if (dse_pref)
		g_pcjs->WriteIntegerL(PrefsCollectionJS::DelayedScriptExecution, dse_pref);
#endif // DELAYED_SCRIPT_EXECUTION
}

html
{
	//! <html><body><form>
	//!  <p>
	//!   <input name="a" type="number" step="0.01" value="0"> *
	//!   <input name="b" type="number" step="0.01" value="0"> =
	//!   <output id="res" name="result"
    //!         onformchange="value = a.value * b.value">0</output>
	//!  </p>
	//! </form></body></html>
}

test("WebForms2.Output.Tag1") language C++;
{
	HTML_Element* output_elm = find_element("output");
	verify(output_elm);
	verify(output_elm->Type() == HE_OUTPUT);
}

test("WebForms2.Output.Tag2part1")
{
	var f = document.forms[0];
	verify(f);
	verify(f.a);
	verify(f.b);
	var a = f.a;
	var b = f.b;

	var o = document.getElementById("res");
	verify(o);

	verify(o.value == "0");

	a.value = 2;
	b.value = 4;
	f.dispatchFormChange();
}
//Wait until the event has been handled
test("WebForms2.Output.Tag2part2") require success "WebForms2.Output.Tag2part1";
{
	var o = document.getElementById("res");
	verify(o.value == 8);

	var f = document.forms[0];
	verify(f);
	verify(f.a);
	var a = f.a;
	a.value = 3;
	verify(f.dispatchFormChange);
	f.dispatchFormChange();
}

//Wait until the event has been handled
test("WebForms2.Output.Tag2part3") require success "WebForms2.Output.Tag2part2";
{
	var o = document.getElementById("res");
	verify(o);
	verify(o.value == 12);
}

html
{
	//! <html><body><form>
	//!  <p>
	//!   <output id="res" name="result"
	">foo<script>document.fooo = document.getElementById('res').defaultValue;\n</script>bar</output>"
	//!  </p>
	//! </form></body></html>
}

test("WebForms2.Output.InitialDefaultValue")
{
	verify("fooo" in document);
	verify(document.fooo == "");
	var out = document.getElementById("res");
	verify(out);
	verify(out.defaultValue == "foodocument.fooo = document.getElementById('res').defaultValue;\nbar" ||
		out.defaultValue == "foobar");
}

test("WebForms2.Output.Value")
{
	verify("fooo" in document);
	verify(document.fooo == "");
	var out = document.getElementById("res");
	verify(out);
	verify(out.defaultValue == "foodocument.fooo = document.getElementById('res').defaultValue;\nbar" ||
		out.defaultValue == "foobar");
	verify(out.value == "foodocument.fooo = document.getElementById('res').defaultValue;\nbar" ||
		out.value == "foobar");
	out.value = "senastelaget";
	verify(out.value == "senastelaget");
}

test("WebForms2.Output.Reset")
{
	var out = document.getElementById("res");
	verify(out);
	verify(out.defaultValue == "foodocument.fooo = document.getElementById('res').defaultValue;\nbar" ||
		out.defaultValue == "foobar");
	out.value = "senastelaget";
	verify(out.value == "senastelaget");
	verify(document.forms[0]);
	var f = document.forms[0];
	verify("reset" in f);
	verify(f.reset);
	f.reset();
	verify(out.value == "foodocument.fooo = document.getElementById('res').defaultValue;\nbar" ||
		out.value == "foobar");
	out.defaultValue = "sandra";
	verify(out.defaultValue == "sandra");
	verify(out.value == "foodocument.fooo = document.getElementById('res').defaultValue;\nbar" ||
		out.value == "foobar");

	f.reset();
	verify(out.defaultValue == "sandra");
	verify(out.value == "sandra");
}

test("WebForms2.Output.Submit")
{
	var out = document.getElementById("res");
	verify(out);

	verify("validity" in out);
	verify("valid" in out.validity);
	verify(out.validity.valid); // no error flags can be set
}

html
{
	//! <html><body><form>
	//! <p><output id="res" name="result">Sofia</output></p>
	//! </form></body></html>
}

test("WebForms2.Output.DefaultValue")
{
	var out = document.getElementById("res");
	verify(out);
	verify(out.defaultValue == "Sofia");
	var oldValue = out.value;
	out.defaultValue = "Hoff";
	verify(out.defaultValue == "Hoff");
	verify(out.value == oldValue);
}
