/* -*- Mode: c++; tab-width: 4; indent-tabs-mode: t; c-basic-offset: 4 -*- */
group "WebForms2.Date";
// require init;

language C++;

include "modules/forms/datetime.h";

subtest CompareDoubles(double got, double expected)
{
	BOOL ok;
	if (op_fabs(expected) < 1.0)
	{
		// Compare diff
		ok = op_fabs(got-expected) < 0.00001;
	}
	else
	{
		ok = op_fabs(got/expected - 1.0) < 0.00001;
	}

	if (!ok)
	{
		OpString8 msg;
		msg.AppendFormat("Wrong number. Expected %f, got %f", expected, got);
		verify(ok, msg.CStr());
	}
}

test("DaySpec.AsDouble")
{
	DaySpec d1 = { 1970, 0, 1 };
	verify(CompareDoubles(d1.AsDouble(), 0.0));

	DaySpec d2 = { 1970, 0, 20 };
	verify(CompareDoubles(d2.AsDouble(), 19.0));

	DaySpec d3 = { 1970, 1, 1 };
	verify(CompareDoubles(d3.AsDouble(), 31.0));

	DaySpec d4 = { 1970, 1, 2 };
	verify(CompareDoubles(d4.AsDouble(), 32.0));

	DaySpec d5 = { 1970, 2, 1 };
	verify(CompareDoubles(d5.AsDouble(), 59.0));

	DaySpec d6a = { 1971, 0, 1 };
	verify(CompareDoubles(d6a.AsDouble(), 365.0));

	DaySpec d6b = { 1969, 11, 31 };
	verify(CompareDoubles(d6b.AsDouble(), -1.0));

	DaySpec d6c = { 1969, 11, 1 };
	verify(CompareDoubles(d6c.AsDouble(), -31.0));

	DaySpec d6d = { 1968, 11, 1 };
	verify(CompareDoubles(d6d.AsDouble(), -396.0));

	DaySpec d6e = { 1967, 11, 31 };
	verify(CompareDoubles(d6e.AsDouble(), -732.0));

	DaySpec d6f = { 1968, 0, 1 };
	verify(CompareDoubles(d6f.AsDouble(), -731.0));

	DaySpec d7 = { 1968, 0, 20 };
	verify(CompareDoubles(d7.AsDouble(), -712.0));

	DaySpec d8 = { 1968, 1, 1 };
	verify(CompareDoubles(d8.AsDouble(), -700.0));

	DaySpec d9 = { 1968, 1, 2 };
	verify(CompareDoubles(d9.AsDouble(), -699.0));

	DaySpec d10 = { 1968, 2, 1 };
	verify(CompareDoubles(d10.AsDouble(), -671.0));
}

test("WeekSpec.AsDouble")
{
	WeekSpec w;
	verify(w.SetFromISO8601String(UNI_L("1970-W01")));
	verify(CompareDoubles(w.AsDouble(), 0.0));

	verify(w.SetFromISO8601String(UNI_L("1970-W02")));
	verify(CompareDoubles(w.AsDouble(), 1.0));

	verify(w.SetFromISO8601String(UNI_L("1971-W01")));
	verify(CompareDoubles(w.AsDouble(), 53.0));

	verify(w.SetFromISO8601String(UNI_L("1971-W02")));
	verify(CompareDoubles(w.AsDouble(), 54.0));

}

test("DaySpec.DayOfWeek")
{
	DaySpec d1 = { 2006, 7, 24 };
	verify(d1.DayOfWeek() == 3); // thursday

	DaySpec d2 = { 1606, 7, 24 };
	verify(d2.DayOfWeek() == 3);

	DaySpec d3 = { 2406, 7, 24 };
	verify(d3.DayOfWeek() == 3);

	DaySpec d4 = { 6006, 7, 24 };
	verify(d4.DayOfWeek() == 3);

	DaySpec d5 = { 2006, 7, 20 };
	verify(d5.DayOfWeek() == 6); // sunday

	DaySpec d6 = { 2006, 7, 21 };
	verify(d6.DayOfWeek() == 0); // monday

	DaySpec d7 = { 2000, 0, 1 };
	verify(d7.DayOfWeek() == 5); // saturday

	DaySpec d8 = { 1971, 0, 1 };
	verify(d8.DayOfWeek() == 4); // friday

	DaySpec d9 = { 1970, 11, 31 };
	verify(d9.DayOfWeek() == 3); // thursday

	DaySpec d10 = { 1970, 0, 4 };
	verify(d10.DayOfWeek() == 6); // sunday

	DaySpec d11 = { 1971, 0, 4 };
	verify(d11.DayOfWeek() == 0); // monday

	DaySpec d12 = { 1901, 0, 1 };
	verify(d12.DayOfWeek() == 1); // tuesday

	DaySpec d13 = { 1900, 11, 31 };
	verify(d13.DayOfWeek() == 0); // monday
}

html
{
	//! <html><form>
	//! <input type="date" value="1970-01-01">
	//! </form></html>
}

test("WebForms2.Date.valueAsNumber")
	language ecmascript;
{
	verify(document);
	verify(document.forms);
	verify(document.forms[0]);
	var f = document.forms[0];
	verify(f[0]);
	var input = f[0];

	verify(input.value == "1970-01-01");
	verify(input.valueAsNumber == 0);

	input.value="1970-01-02";
	verify(input.value == "1970-01-02");
	verify(input.valueAsNumber == 86400*1000);

	input.value="1970-01-03";
	verify(input.value == "1970-01-03");
	verify(input.valueAsNumber == 2*86400*1000);
}

html
{
	//! <html><form>
	//! <input type="date" value="1972-01-03" step="1" min="1971-12-31" max="1972-01-06">
	//! </form></html>
}

test("WebForms2.Date.stepUpDown#1")
	language ecmascript;
{
	verify(document);
	verify(document.forms);
	verify(document.forms[0]);
	var f = document.forms[0];
	verify(f[0]);
	var input = f[0];

	verify("stepUp" in input);
	verify("stepDown" in input);

	verify("valueAsNumber" in input);

	var baseDate = (2*365+2)*86400*1000;
	var step = 86400*1000;
	verify(input.valueAsNumber == baseDate);
	verify(input.value == "1972-01-03");
	input.stepUp(1);
	verify(input.value == "1972-01-04");
	verify(input.valueAsNumber == baseDate+step);
	input.stepDown(4);
	verify(input.value == "1971-12-31");
	verify(input.valueAsNumber == baseDate-3*step);
	input.stepUp(3);
	verify(input.value == "1972-01-03");
	verify(input.valueAsNumber == baseDate);
	input.stepDown(2);
	verify(input.value == "1972-01-01");
	verify(input.valueAsNumber == baseDate-2*step);

	try
	{
		input.stepDown(2);
		verify(!"The line above should have thrown an execption");
	}
	catch (e)
	{
		verify (e.code == DOMException.INVALID_STATE_ERR);
	}
	verify(input.value == "1972-01-01");
	verify(input.valueAsNumber == baseDate-2*step);

	input.value = "1972-01-06";
	verify(input.valueAsNumber == baseDate+3*step);
	try
	{
		input.stepUp(1);
		verify(!"The line above should have thrown an execption");
	}
	catch (e)
	{
		verify (e.code == DOMException.INVALID_STATE_ERR);
	}
	verify(input.value == "1972-01-06");
	verify(input.valueAsNumber == baseDate+3*step);
	input.stepDown(0);
	verify(input.value == "1972-01-06");
	input.stepUp(0);
	verify(input.value == "1972-01-06");
}



html
{
	//! <html><form>
	//! <input type="date" value="1972-03-04" step="any" max="1980-03-23" min="1970-12-12">
	//! </form></html>
}

test("WebForms2.Date.stepUpDown#2")
	language ecmascript;
{
	verify(document);
	verify(document.forms);
	verify(document.forms[0]);
	var f = document.forms[0];
	verify(f[0]);
	var input = f[0];

	verify("stepUp" in input);
	verify("stepDown" in input);

	verify("valueAsNumber" in input);

	verify(input.step == "any");
	try
	{
		// step == "any"
		input.stepDown(1);
		verify(!"The line above should have thrown an execption");
	}
	catch (e)
	{
		verify (e.code == DOMException.INVALID_STATE_ERR);
	}
}


html
{
	//! <html><form>
	//! <input type="date" value="1970-01-03" step="7">
	//! </form></html>
}

test("WebForms2.Date.stepUpDown#3")
	language ecmascript;
{
	verify(document);
	verify(document.forms);
	verify(document.forms[0]);
	var f = document.forms[0];
	verify(f[0]);
	var input = f[0];

	verify("stepUp" in input);
	verify("stepDown" in input);

	verify("valueAsNumber" in input);

	verify(input.valueAsNumber == 2*86400*1000);
	input.stepUp(1);
	verify(input.value == "1970-01-10");
	input.stepDown(1);
	verify(input.value == "1970-01-03");
}

html
{
	//! <html><form>
	//! <input type="date" step="3600">
	//! </form></html>
}

test("WebForms2.Date.stepUpDown#4")
	language ecmascript;
{
	verify(document);
	verify(document.forms);
	verify(document.forms[0]);
	var f = document.forms[0];
	verify(f[0]);
	var input = f[0];

	verify("stepUp" in input);
	verify("stepDown" in input);

	verify("valueAsNumber" in input);

	verify(input.value == "");
	try
	{
		// No value
		input.stepDown(1);
		verify(!"The line above should have thrown an execption");
	}
	catch (e)
	{
		verify (e.code == DOMException.INVALID_STATE_ERR);
	}
	try
	{
		// No value
		input.stepUp(1);
		verify(!"The line above should have thrown an execption");
	}
	catch (e)
	{
		verify (e.code == DOMException.INVALID_STATE_ERR);
	}
}

html
{
	//! <html><form>
	//! <input type="date" value="2006-02-12">
	//! </form></html>
}

test("WebForms2.Date.stepUpDown#5")
	language ecmascript;
{
	verify(document);
	verify(document.forms);
	verify(document.forms[0]);
	var f = document.forms[0];
	verify(f[0]);
	var input = f[0];

	verify("stepUp" in input);
	verify("stepDown" in input);

	verify("valueAsNumber" in input);

	verify(input.value == "2006-02-12");
	input.stepDown(1); // default step
	verify(input.value == "2006-02-11");
	input.stepDown(11); // default step
	verify(input.value == "2006-01-31");
}
