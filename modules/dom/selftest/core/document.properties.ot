group "DOM.Core.Document.properties";
require init;

language ecmascript;

// A non-html document to test in
xml
{
	"<foo/>"
}

test("document.cookie - existence")
{
	verify(document);
        verify("cookie" in document);
        verify(document.cookie == "");
}

test("document.location - existence")
{
        verify(document);
        verify("location" in document);
        verify(document.location);

        var freedoc = document.implementation.createDocument (null, null, null);
        verify(freedoc);
        verify("location" in freedoc);
        verify(!freedoc.location);
}

html
{
	//! <body>
	//! <p>FAIL</p>
	//! <script type="text/javascript">
	//! function test(){
	//!		var log = document.getElementsByTagName('p')[0].firstChild;
	//! 	log.data = document.readyState;
	//! 	var script = document.createElement('script');
	//! 	script.src = 'http://testsuites.oslo.opera.com/resources/scripts/delay.php?url=http://testsuites.oslo.opera.com/resources/scripts/nothing.js&time=3';
	//! 	document.getElementsByTagName('head')[0].appendChild(script);
	//! 	log.data += ' '+document.readyState;
	//! }
	//! window.onload=test;
	//! </script>
	//! </body>
}

test("document.readyState shouldn't revert back to interactive")
{
	// CORE-27693
	verify(document);
	verify(document.getElementsByTagName('p')[0].firstChild.data === 'complete complete');
}


html
{
	//! <head>
	//! <title>summary</title>
	//! </head>
	//! <body>
	//! <p>PASS</p>
	//! </body>
}
test("document.title - settable, if exist.")
{
	verify(document);
	verify("title" in document);
	verify(document.title == "summary");
	document.title = "summary2";
	verify(document.title == "summary2");
}

html
{
	//! <head>
	//! </head>
	//! <body>
	//! <p>PASS</p>
	//! </body>
}
test("document.title - settable, if non-existing  #1")
{
	verify(document);
	verify("title" in document);
	verify(document.title == "");
	document.title = "summary";
	verify(document.title == "summary");
	document.title = "summary2";
	verify(document.title == "summary2");
}

html
{
	//! <body>
	//! <p>PASS</p>
	//! </body>
}

test("document.title - settable, if non-existing  #2")
{
	verify(document);
	verify("title" in document);
	verify(document.title == "");
	document.title = "summary";
	verify(document.title == "summary");
	document.title = "summary2";
	verify(document.title == "summary2");
}

html
{
	//! <head>
	//! <title>summary  </title>
	//! </head>
	//! <body>
	//! <p>PASS</p>
	//! </body>
}
test("document.title - value sanitization.")
{
	verify(document);
	verify("title" in document);
	verify(document.title == "summary");
        var str = "sum\n\nmary2  ";
	document.title = str;
	verify(document.title == "sum mary2");
}

/**
 * Unfortunatelly the following tests are not exhaustive.
 * Multiple scenarios would need to be tested like XHR,
 * file uris, http requests with and without mime-type, etc.
 * See CORE-36588 for details.
 */
html "http://foo.bar/"

test("document.contentType text/html 1")
{
	verify(document);
	verify(document.contentType == "text/html");
}

test("document.contentType + createDocument")
{
	var d = document.implementation.createDocument(null, null, null);
	verify(d.contentType == "application/xml");

	var d = document.implementation.createHTMLDocument(null);
	verify(d.contentType == "text/html");
}

test("document.contentType XHR")
{
	var x = new XMLHttpRequest();
	x.open('GET', 'data:application/xml,<r/>', false);
	x.send();

	verify(x.responseText == '<r/>');
	verify(x.responseXML);
	verify(x.responseXML.contentType == 'application/xml');
}

data "text/html" { "<html>" }

test("document.contentType text/html 2")
{
	verify(document);
	verify(document.contentType == "text/html");
}

data "image/png" "PNG"

test("document.contentType image/png")
{
	verify(document);
	verify(document.contentType == "image/png");
}

data "application/xml" { "<xml/>" }

test("document.contentType application/xml")
{
	verify(document);
	verify(document.contentType == "application/xml");
}

xml "file://localhost/home/foobar.xml" "<r/>"

test("document.contentType local file")
{
	verify(document);
	// any of these two is fine
	verify(document.contentType == "application/xml" || document.contentType == "text/xml");
}
