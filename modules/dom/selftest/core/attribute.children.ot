group "DOM.Core.Attribute.children";
require init;

language ecmascript;

xhtml
{
  "<?xml version='1.0'?>"
  "<html xmlns='http://www.w3.org/1999/xhtml'>"
    "<body>"
      "<a href='http://www.opera.com/'>Opera</a>"
    "</body>"
  "</html>"
}

test("Setup")
{
  a_href = document.getElementsByTagName("a").item(0).getAttributeNodeNS(null, "href");
  verify(a_href);
}

test("Attr.firstChild")
  require success "Setup";
{
  verify(a_href.firstChild);
  verify(a_href.firstChild.parentNode == a_href);
  verify(a_href.firstChild.nodeName == "#text");
  verify(a_href.firstChild.nodeValue == "http://www.opera.com/");
}

test("Attr.lastChild")
  require success "Setup";
{
  verify(a_href.lastChild);
  verify(a_href.lastChild.parentNode == a_href);
  verify(a_href.lastChild.nodeName == "#text");
  verify(a_href.lastChild.nodeValue == "http://www.opera.com/");
}

test("Attr.childNodes")
  require success "Setup";
{
  verify(a_href.childNodes);
  verify(a_href.childNodes.length == 1);
  verify(a_href.childNodes.item(0) == a_href.firstChild);
}

test("Attr.textContent")
  require success "Setup";
{
  verify(a_href.textContent == "http://www.opera.com/");
}

test("modifying value via tree")
  require success "Setup";
{
  verify(a_href.value == "http://www.opera.com/");
  a_href.removeChild (a_href.firstChild);
  verify(a_href.value == "");
  a_href.appendChild (document.createTextNode ("http://www.opera.com/"));
  verify(a_href.value == "http://www.opera.com/");
  a_href.appendChild (document.createTextNode ("download/"));
  verify(a_href.value == "http://www.opera.com/download/");
  a_href.removeChild (a_href.firstChild);
  verify(a_href.value == "download/");
  a_href.firstChild.nodeValue = "products/";
  verify(a_href.value == "products/");
  a_href.insertBefore (document.createTextNode ("http://www.opera.com/"), a_href.firstChild);
  verify(a_href.value == "http://www.opera.com/products/");
}

test("modifying tree via value")
  require success "Setup";
{
  a_href.value = "http://www.opera.com/support/";
  verify(a_href.firstChild.nodeValue == "http://www.opera.com/support/");
  var previous_firstChild = a_href.firstChild;
  a_href.value = "http://www.opera.com/";
  verify(a_href.firstChild.nodeValue == "http://www.opera.com/");
  verify(a_href.firstChild != previous_firstChild);
  a_href.appendChild (document.createTextNode ("download/"));
  verify(a_href.childNodes.length == 2);
  a_href.value = "http://www.opera.com/";
  verify(a_href.childNodes.length == 1);
  a_href.value = "";
  verify(a_href.childNodes.length == 0);
  verify(a_href.firstChild == null);
  verify(a_href.lastChild == null);
}

test("inserting element into attribute")
  require success "Setup";
{
  try
    {
      a_href.appendChild(document.createElementNS(null, "element"));
      verify(!"expected exception");
    }
  catch (e)
    {
      verify(e.code == DOMException.HIERARCHY_REQUEST_ERR);
    }

  a_href.value = "";
}

test("inserting comment into attribute")
  require success "Setup";
{
  try
    {
      a_href.appendChild(document.createComment("comment"));
      verify(!"expected exception");
    }
  catch (e)
    {
      verify(e.code == DOMException.HIERARCHY_REQUEST_ERR);
    }

  a_href.value = "";
}

test("inserting cdata into attribute")
  require success "Setup";
{
  try
    {
      a_href.appendChild(document.createCDATASection("cdata"));
      verify(!"expected exception");
    }
  catch (e)
    {
      verify(e.code == DOMException.HIERARCHY_REQUEST_ERR);
    }

  a_href.value = "";
}
