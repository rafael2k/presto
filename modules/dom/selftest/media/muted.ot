/* -*- Mode: js; tab-width: 4; indent-tabs-mode: t; c-basic-offset: 4 -*-
 *
 * Copyright (C) 2011 Opera Software ASA.  All rights reserved.
 *
 * This file is part of the Opera web browser.
 * It may not be distributed under any circumstances.
 */

group "DOM.Media.Muted";
require MEDIA_HTML_SUPPORT;
require init;
language ecmascript;

html
{
	//! <audio></audio>
	//! <audio muted></audio>
	//! <video></video>
	//! <video muted></video>
}

table TagNames
{
	{"audio"}, {"video"}
}

foreach (tagName) from TagNames
{
	test("<" tagName ">")
	{
		var e = document.querySelector(tagName);
		verify(e.muted === false);
		verify(e.defaultMuted === false);
	}

	test("<" tagName " muted>")
	{
		var e = document.querySelector(tagName+"[muted]");
		verify(e.muted === true);
		verify(e.defaultMuted === true);
	}

	test(tagName ".setAttribute(\"muted\", \"\")")
	{
		var e = document.createElement(tagName);
		e.setAttribute("muted", "");
		verify(e.muted === false);
		verify(e.defaultMuted === true);
	}

	test(tagName ".removeAttribute(\"muted\")")
	{
		var e = document.createElement(tagName);
		e.defaultMuted = true;
		e.removeAttribute("muted");
		verify(e.muted === false);
		verify(e.defaultMuted === false);
	}

	test(tagName ".defaultMuted")
	{
		var e = document.createElement(tagName);
		e.defaultMuted = true;
		verify(e.hasAttribute("muted"));
		e.defaultMuted = false;
		verify(!e.hasAttribute("muted"));
	}
}
