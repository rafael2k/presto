// -*- Mode: c++; tab-width: 4; indent-tabs-mode: t; c-basic-offset: 4 -*-
group "logdoc.writingsystem";

require FONTSWITCHING;

include "modules/logdoc/htm_ldoc.h";
include "modules/windowcommander/WritingSystem.h";
include "modules/doc/frm_doc.h";
include "modules/dochand/docman.h";
include "modules/windowcommander/src/WindowCommander.h";
include "modules/selftest/src/doc_st_utils.h";

html
{
//! <html lang="ru">
//! <head>
//! <meta http-equiv="content-type" content="text/html;charset=utf-8">
//! </head>
//! <body>
//! </body>
//! </html>
}
test("from lang")
{
	HLDocProfile* hl_doc = state.doc->GetHLDocProfile();
	verify(hl_doc);
	verify(hl_doc->GetPreferredScript() == WritingSystem::Cyrillic);
}

xml
{
//! <?xml version="1.0" encoding="UTF-8"?>
//! <html xmlns="http://www.w3.org/1999/xhtml" xml:lang="ru">
//! <head>
//! </head>
//! <body>
//! </body>
//! </html>
}
test("from xml:lang")
{
	HLDocProfile* hl_doc = state.doc->GetHLDocProfile();
	verify(hl_doc);
	verify(hl_doc->GetPreferredScript() == WritingSystem::Cyrillic);
}

table dummy_files(char *) filelist "files" name "*.html";
table test_files(char*, WritingSystem::Script)
{
	{ "files/latin1.html", WritingSystem::LatinWestern } // charset=ISO-8859-1
	{ "files/ru.html", WritingSystem::Cyrillic }         // charset=Windows-1251
	{ "files/sjis.html", WritingSystem::Japanese }       // charset=Shift_JIS
	{ "files/sjis_ru.html", WritingSystem::Cyrillic }    // charset=Shift_JIS, lang="ru"
}

// Open a window for test documents
html
{
	//! <body>
	//! </body>
}
foreach (file, script) from test_files
{
	test("loadpage_dummy") async;
	{
		OpString f;
		if (OpStatus::IsError(f.Set(ST_make_path(file))))
			ST_failed("OOM");
		else
		{
			Window* win = state.GetWindow();
			if (!win)
				ST_failed("No window!");
			else
				WaitUntilLoadedObj::LoadAndWaitUntilLoaded(win, f.CStr());
		}
	}
	test("from encoding - " file)
	{
		HLDocProfile* hl_doc = state.doc->GetHLDocProfile();
		verify(hl_doc);
		verify(hl_doc->GetPreferredScript() == script);
	}
}
