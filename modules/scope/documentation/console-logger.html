<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" 
          "http://www.w3.org/TR/html4/loose.dtd">
<html>

<head>
<link rel="stylesheet" href="scope.css" />
<title>Console Logger</title>
<script type="text/javascript" src="markup_grammar.js"></script>
</head>

<body onload="markup_grammar()">
<h1>Console Logger</h1>

<h2>Overall idea</h2>

<p> The <tt>console-logger</tt> is a noninteractive tool used to log
messages sent to Opera's console.</p>

  <p> All messages sent to the Opera console from the various
  modules in Opera are captured and displayed.</p>

  <p> A logged datum has six parts:</p>
<ul>
  <li>time</li>
  <li>uri</li>
  <li>context</li>
  <li>source</li>
  <li>severity</li>
  <li>window</li>
  <li>text</li>
</ul>

<h2>BNF</h2>

<p> Here is the grammar describing the data.  Some of the data elements
are described in more detail below.</p>

<pre id="grammar">
  ###
  # The protocol is a one-way XML-based protocol, with data flowing from the
  # host to the debugger client.
  #
  # The service name of the Console logger is "console-logger".
  #

  PROTOCOL ::= MESSAGE* ;

  MESSAGE ::= "&lt;message&gt;"
                TIME
                URI?
                CONTEXT?
                SOURCE?
                SEVERITY?
                WINDOW-ID
                DESCRIPTION
              "&lt;/message&gt;"
              ;

  # The number of seconds since 1970-01-01T00:00:00Z.
  
  TIME ::= "&lt;time&gt;"
             UNSIGNED
           "&lt;/time&gt;"
           ;

  # The URL of the entity (document, script, style file, etc) that caused the
  # message to be generated.

  URI ::= "&lt;uri&gt;"
            TEXT
          "&lt;/uri&gt;"
          ;

  # The context in which the message was generated; it is intended for human
  # consumption and provides additional information about the context for the
  # message.

  CONTEXT ::= "&lt;context&gt;"
                TEXT
              "&lt;/context&gt;"
              ;

  # The component in Opera that generated the message

  SOURCE ::= "&lt;source&gt;"
               ( "ecmascript" | "java" | "m2" | "network" | "xml" | "html" |
                 "css" | "xslt" | "svg" | "bittorrent" | "voice" | "widget" |
                 "selftest" )
             "&lt;/source&gt;"
             ;

  SEVERITY ::= "&lt;severity&gt;"
                 ( "debug" | "verbose" | "information" | "error" | "critical" )
               "&lt;/severity&gt;"
               ;

  # Description of the error. Sometimes this message only makes sense when
  # displayed in a monospace font.

  DESCRIPTION ::= "&lt;description&gt;"
                    TEXT
                  "&lt;/description&gt;"
                  ;

  # The window ID is shared across scope. Notably, it's the same as in the ecmascript debugger.
  # INTERNAL: The value is from Window::id

  WINDOW-ID ::= "&lt;window-id&gt;" UNSIGNED "&lt;/window-id&gt;" ;

  ###
  # Primitive data.
  #
  # You may *NOT* assume that an UNSIGNED received from the host fits 
  # in 32 bits, but you may assume that 64 bits is enough.
  #
  # You must *NOT* send an UNSIGNED to the host that does not fit in 32
  # bits unless it was received from the host.
  #

  UNSIGNED ::= [0-9]+ ;

  TEXT ::= BASE64-ENCODED-DATA | textual-data ;

  BASE64-ENCODED-DATA ::= "&lt;base64-encoded-data&gt;" textual-data "&lt;/base64-encoded-data&gt;" ;
</pre>

</body>

</html>
