<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
          "http://www.w3.org/TR/html4/loose.dtd">
<html>

<head>
<link rel="stylesheet" href="scope.css" />
<title>Opera Exec protocol</title>

<style type="text/css">
h3 { font-weight: bold; font-style: italic }
h3::before { font-style: normal }
</style>

<script type="text/javascript" src="markup_grammar.js"></script>
</head>

<body onload="markup_grammar()">
<h1>Opera Exec protocol</h1>

<p>The Opera Exec protocol can be used to control an Opera instance from
the outside, and various operations can be initiated.  This functionality is
mainly useful for QA testing.</p>

<h2>BNF</h2>

<p> This is the grammar describing the data.</p>

<pre id="grammar">

#
# Commands (messages sent to the Opera instance to be manipulated)
#

PROTOCOL ::= (COMMAND | EVENT)*;

COMMAND ::= EXEC
          | GET-ACTION-INFO-LIST
          | SCREEN-WATCHER
          ;

EVENT ::= ACTION-INFO-LIST
        | SCREEN-WATCHER-REPLY
        ;

EXEC ::= "&lt;exec&gt;" OPERATION+ "&lt;/exec&gt;" ;

# Although the EBNF does not reflect this, only one MOUSE-ACTION is
# allowed per EXEC command.

OPERATION ::= KEYDOWN
            | KEYUP
            | TYPE
            | ACTION
            | MOUSE-ACTION
            ;

KEYDOWN ::= "&lt;keydown&gt;"
            KEY
            "&lt;/keydown&gt;"
            ;

KEYUP ::= "&lt;keyup&gt;"
          KEY
          "&lt;/keyup&gt;"
          ;

TYPE ::= "&lt;type&gt;"
         TEXT
         "&lt;/type&gt;"
         ;

ACTION ::= "&lt;action&gt;"
           NAME
           PARAM?
           WINDOW-ID?
           "&lt;/action&gt;"
           ;

# PARAM to the action. E.g. "opera.com" to the command "go"
PARAM ::= "&lt;param&gt;"
          TEXT
          "&lt;/param&gt;"
          ;

WINDOW-ID ::= "&lt;window-id&gt;"
              UNSIGNED
              "&lt;/window-id&gt;"
              ;

# The KEY is either a key-name ("ctrl", "down", etc.)
# or a single character ("a", "b", etc.)

KEY ::= TEXT ;

# The ACTION-NAME is the name of an Action ("Page Down", "New Page", etc.)

ACTION-NAME ::= TEXT ;

# Request a list of valid actions. The reply will come as an ACTION-INFO-LIST

GET-ACTION-INFO-LIST ::= "&lt;get-action-info-list /&gt;" ;

ACTION-INFO-LIST ::= "&lt;action-info-list&gt;"
                        NAME*
                     "&lt;/action-info-list&gt;"
                     ;

NAME ::= "&lt;name&gt;"
            ACTION-NAME
         "&lt;/name&gt;"
         ;


# Defaults:
#   WINDOW-ID:  current window
#   TIMEOUT:    150000 (15 seconds)
#   AREA:       x=0, y=0, w=200, h=100

SCREEN-WATCHER ::= "&lt;screen-watcher&gt;"
                     WINDOW-ID?
                     TIMEOUT?
                     AREA?
                     MD5*
                     COLOR-SPEC*
                     INCLUDE-IMAGE?
                  "&lt;/screen-watcher&gt;" ;

# Timeout in milliseconds
# If omitted, defaults to 15 seconds

TIMEOUT ::= "&lt;timeout&gt;" UNSIGNED "&lt;/timeout&gt;" ;

# Define an area on the page, relative to the page (not viewport)

AREA ::= "&lt;area&gt;"
            "&lt;x&gt;" UNSIGNED "&lt;/x&gt;" # horizontal offset
            "&lt;y&gt;" UNSIGNED "&lt;/y&gt;" # vertical offset
            "&lt;w&gt;" UNSIGNED "&lt;/w&gt;" # width
            "&lt;h&gt;" UNSIGNED "&lt;/h&gt;" # height
            "&lt;viewport-relative /&gt;"     # are the coordinates relative to viewport or document
         "&lt;/area&gt;" ;

# MD5 sum of an image, in hexadecimal

MD5 ::= "&lt;md5&gt;" "0x" [0-9a-f]{32} "&lt;/md5&gt;" ;

# The reply SCREEN-WATCHER-REPLY will include a count of all the pixles for the
# various id's given in the colspecs. You can have multiple colspecs with the
# same id - the report will then include the total count of all the colspecs
# for that id. You can also have overlapping color specifications.
#
# Note: There can be maximum 16 colorspecs!
#
# Any LOW-* element missing will default to 0
# Any HIGH-* element missing will default to 255
#
# Returns SCREEN-WATCHER-REPLY

COLOR-SPEC ::= "&lt;color-spec&gt;"
                  ID
                  LOW-RED?
                  HIGH-RED?
                  LOW-GREEN?
                  HIGH-GREEN?
                  LOW-BLUE?
                  HIGH-BLUE?
               "&lt;/color-spec&gt;" ;

# ID is used by COLOR-SPEC to identify a color. Does not need to be unique

ID ::= "&lt;id&gt;" UNSIGNED "&lt;/id&gt;" ;

# Specify whether to include the image ("yes") or not ("no"). Can be used to
# reduce bandwith usage in applications where only hashes are needed.
#
# Default is YES.

INCLUDE-IMAGE ::= "&lt;include-image&gt;"
                     (
                        "&lt;yes /&gt;"
                        "&lt;no /&gt;"
                     )
                  "&lt;include-image&gt;" ;

# Sent as reply to SCREEN-WATCHER command.

SCREEN-WATCHER-REPLY ::= "&lt;screen-watcher-reply&gt;"
                            WINDOW-ID
                            MD5
                            COLOR-MATCH*
                         "&lt;/screen-watcher-reply&gt;" ;

# The count of color IDs that matched (ID is from COLOR-SPEC).

COLOR-MATCH ::= "&lt;color-match&gt;"
                   ID
                   "&lt;count&gt;" UNSIGNED "&lt;/count&gt;"
                "&lt;/color-match&gt;" ;

###
# Color values ranging from 0 (no color) to 255 (maximal saturation).
# Default (meaning element missing) is 0 for LOW-* elements and 255 for HIGH-* elements.
#

LOW-RED    ::= "&lt;low-red&gt;"    UNSIGNED "&lt;/low-red&gt;" ;
HIGH-RED   ::= "&lt;high-red&gt;"   UNSIGNED "&lt;/high-red&gt;" ;
LOW-GREEN  ::= "&lt;low-green&gt;"  UNSIGNED "&lt;/low-green&gt;" ;
HIGH-GREEN ::= "&lt;high-green&gt;" UNSIGNED "&lt;/high-green&gt;" ;
LOW-BLUE   ::= "&lt;low-blue&gt;"   UNSIGNED "&lt;/low-blue&gt;" ;
HIGH-BLUE  ::= "&lt;high-blue&gt;"  UNSIGNED "&lt;/high-blue&gt;" ;

# Moved mouse to the given position on the screen/window.
# Note that the mouse cursor is not moved visibly.
#
# The coordinates are relative to the upper left corner of the tab
# (not including chrome).
#
# Only one MOUSE-ACTION can be sent per EXEC command.

MOUSE-ACTION ::= "&lt;mouse-action&gt;"
                    WINDOW-ID
                    "&lt;x&gt;" UNSIGNED "&lt;/x&gt;"
                    "&lt;y&gt;" UNSIGNED "&lt;/y&gt;"
                    BUTTONS
                 "&lt;/mouse-action&gt;"
                 ;

# BUTTONS specifies the buttons to press or release
# It is specifies as the sum of button actions:
#       1 - Button 1 down
#       2 - Button 1 up
#
#       4 - Button 2 down
#       8 - Button 2 up
#
#      16 - Button 3 down
#      32 - Button 3 up
#
# For example, to press button 1 and release button 2, the value is 9 (1+8)
#
# Buttons are clicked in the sequence listed above. Note that down actions are
# listed before up actions, thus allowing single-clicking with one command
# (e.g. using value 3)

BUTTONS ::= "&lt;buttons&gt;" UNSIGNED "&lt;/buttons&gt;" ;

###
# From ecmascript-debugger-protocol.html
#

UNSIGNED ::= [0-9]+ ;

TEXT ::= BASE64-ENCODED-DATA | textual-data ;

BASE64-ENCODED-DATA ::= "&lt;base64-encoded-data&gt;" textual-data "&lt;/base64-encoded-data&gt;" ;
</pre>

<h2>Examples</h2>

<p>The following example will do a spatial navigation down:</p>

<pre>
&lt;exec&gt;
  &lt;keydown&gt;shift&lt;/keydown&gt;&lt;keydown&gt;down&lt;/keydown&gt;
  &lt;keyup&gt;down&lt;/keyup&gt;&lt;keyup&gt;shift&lt;/keyup&gt;
&lt;/exec&gt;
</pre>

<p>The following will type the text "Hello":</p>

<pre>
&lt;exec&gt;&lt;type&gt;Hello&lt;/type&gt;&lt;/exec&gt;
</pre>

<p> Take note that shift-modifier key-presses are neither generated
nor taken into account when processing the letters of the TYPE
operation. This means that no shift key will be pressed around the 'H'
in 'Hello' above, and the text typed would still be 'Hello' if the
shift key was pressed (with a KEYDOWN operation) before the TYPE
operation.</p>

<p>The following will trigger an Action of "Page down":</p>

<pre>
&lt;exec&gt;&lt;action&gt;Page Down&lt;/action&gt;&lt;/exec&gt;
</pre>

<h2>Error handling</h2>

<p>There are currently no error messages or other reports of a failed
operation.  Incorrect syntax, unknown keys, or other invalid input will
be silently dropped.</p>


</body>
</html>
