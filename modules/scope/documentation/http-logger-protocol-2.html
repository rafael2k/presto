<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" 
          "http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>HTTP Logger</title>
<link rel=stylesheet href="scope.css" />
<style>
h3 { font-weight: bold; font-style: italic }
h3::before { font-style: normal }
</style>
<script src="markup_grammar.js"></script>
</head>

<body onload="markup_grammar()">
<h1>HTTP Logger Protocol, version 2</h1>

<p>
   The <tt>http-logger</tt> is a noninteractive tool used to log interactions between Opera and the web servers it accesses.</p>

<p>
   This is a draft which has never been implemented.

<h2>BNF</h2>

<p> Following is the grammar of the service, "http-logger":
<pre id="grammar">
  PROTOCOL ::= (COMMAND | EVENT)* ;


  ##
  # Commands (messages from debugger to debugging host)
  #

  COMMAND ::= SET-LOG-MODE
            | GET-LOG-MODE
            | GET-RESPONSE-BODY
            ;

  SET-LOG-MODE ::= "&lt;set-log-mode&gt;" LOG-MODE-TYPE "&lt;/set-log-mode&gt;" ;

  GET-RESPONSE-BODY ::= "&lt;get-response-body&gt;"
                          REQUEST-ID
                          ENCODING?
                          DECODING?
                        "&lt;/get-response-body&gt;"
                        ;

  GET-LOG-MODE ::= "&lt;get-log-mode /&gt;" ;

  ###
  # Events (messages from debugging host to debugger)
  #

  EVENT ::= REQUEST
          | RESPONSE-HEADER
          | RESPONSE-BODY
          | LOG-MODE
          ;

  REQUEST ::= "&lt;request&gt;"
                REQUEST-ID
                WINDOW-ID
                RUNTIME-ID # this is perhaps not necessary if we have frame path
                FRAME-PATH
                TIME
                METHOD
                URL
                PARSED-HEADERS
              "&lt;/request&gt;" ;

  # The RESPONSE does not have it's own id, but refers to the generating REQUEST-ID

  RESPONSE-HEADER ::= "&lt;response-header&gt;"
                        REQUEST-ID
                        TIME
                        MIME-TYPE
                        ENCODING
                        STATUS
                        RAW-HEADER
                        PARSED-HEADERS
                      "&lt;/response-header&gt;" ;

  # TODO: How should an event stream (e.g. server sent events) be represented? How about a really slow connection or big file?
  
  RESPONSE-BODY ::= "&lt;response-body&gt;"
                      REQUEST-ID
                      TIME
                      BODY?
                    "&lt;/response-body&gt;" ;
  
  LOG-MODE ::= "&lt;log-mode&gt;" LOG-MODE-TYPE "&lt;/log-mode&gt;" ;

  ##
  # Other data types
  #

  BODY ::= "&lt;body&gt;"
             ENCODING
             DECODING
             BODY-DATA
           "&lt;/body&gt;";

  # REQUEST-ID is an identifier for the request; it is not necessarily unique across
  # time, as it is just the memory address of the request object. 

  REQUEST-ID ::= "&lt;request-id&gt;" UNSIGNED "&lt;/request-id&gt;" ;

  # Timestamp of the message, in milliseconds since 1970-01-01 00:00 UTC
  # (be sure not to rely on the time being correct - this is client time, and
  # may be wildly different than the host time)

  TIME ::= "&lt;time&gt;" FLOAT "&lt;/time&gt;" ;

  # Header of the request/response, i.e. the HTTP data up until the first
  # occurence of CR+LF+CR+LF, inclusive.

  RAW-HEADER ::= "&lt;raw-header&gt;" TEXT "&lt;/raw-header&gt;" ;
  
  PARSED-HEADERS ::= "&lt;parsed-headers&gt;" HEADER* "&lt;/parsed-headers&gt;" ;

  HEADER ::= "&lt;header&gt;"
               HEADER-NAME
               HEADER-VALUE 
             "&lt;/header&gt;" ;

  HEADER-NAME ::= "&lt;header-name&gt;" TEXT "&lt;/header-name&gt;" ;

  HEADER-VALUE ::= "&lt;header-value&gt;" TEXT "&lt;/header-value&gt;" ;

  ENCODING ::= "&lt;encoding&gt;" ENCODING-TYPE "&lt;/encoding&gt;" ;

  DECODING ::= "&lt;decoding&gt;" DECODING-TYPE "&lt;/decoding&gt;" ;
  
  BODY-DATA ::= "&lt;body-data&gt;" TEXT "&lt;/body-data&gt;" ;

  FLOAT ::= [0-9]+ "." [0-9]+ ;

  # TODO: These names does not really reflect what is going on. At the very
  #       least they need an explanation.

  LOG-MODE-TYPE ::= "profiling" | "logging" ;

  # DECODING-TYPE is either 
  #   "none", meaning raw data, or
  #   "used-by-opera", which is how Opera ended up decoding it
  # TODO: How will Opera tell the client which encoding it actually used?

  DECODING-TYPE ::= TEXT ;

  ENCODING-TYPE ::= "none" | "base64" ;

  FRAME-PATH ::= TEXT ;

  MIME-TYPE ::= TEXT ;

  STATUS ::= UNSIGNED ;
  
  METHOD ::= TEXT ;
  
  URL ::= TEXT ;

  ###
  # From ecmascript-debugger-protocol.html
  #

  # The window ID is shared across scope. Notably, it's the same as in the ecmascript debugger.
  # INTERNAL: The value is from Window::id

  WINDOW-ID ::= "&lt;window-id&gt;" UNSIGNED "&lt;/window-id&gt;" ;

  UNSIGNED ::= [0-9]+ ;
  
  RUNTIME-ID ::= UNSIGNED ; 

  TEXT ::= textual-data ;
</pre>
</body>
</html>
