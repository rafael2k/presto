<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
          "http://www.w3.org/TR/html4/loose.dtd">

<html>

<head>

    <link rel="stylesheet" href="scope.css" />

    <title>Window Manager Service</title>



    <script type="text/javascript" src="markup_grammar.js"></script>



</head>

<body onload="markup_grammar()">

    <h1>

        Window Manager Service</h1>

    <h2>

        Overall idea</h2>

    <p>

        We want to be able to work with windows on a higher level than before. One important goal is to opt-in on the messages you

        get instead of being flooded.</p>

    <p>

        The name of this service is <code>window-manager</code>.</p>

    <h2>

        BNF</h2>

    <p>

        Here is the grammar describing the data. Some of the following data elements are described in more detail.</p>

    <p>

        This is the second version of the window-manager, version 2.</p>

    <pre id="grammar">
  ###
  # A grammar for listing and filtering windows
  #

  PROTOCOL ::= (COMMAND | EVENT)* ;

  COMMAND ::= GET-ACTIVE-WINDOW
            | LIST-WINDOWS
            | FILTER
            ;

  EVENT ::= LIST-WINDOWS-REPLY
          | ACTIVE-WINDOW
          | UPDATED-WINDOW
          | WINDOW-CLOSED
          | FILTER-REPLY        # Introduced in version 2
          ;

  ###
  # Commands. Sent from client.
  #

  # The default FILTER used to be INCLUDE "*".
  # After this command was introduced, the default filter is INCLUDE WINDOW-ID 0 (unknown).
  #
  # The filtering mechanism works by taking the appropriate window-id of the message and checks it.
  #   If the INCLUDE filter contains the window-id, or a window that has opened the window,
  #   and the window-id is not in the EXCLUDE filter,
  #     then send the message.
  #   Otherwise,
  #     discard the message
  #
  # INCLUDE and EXCLUDE appends to the existing filter. This is true whether these occur in the
  # same FILTER command or occur in different FILTER commands. If the CLEAR flag is present, the
  # filter will cleared before the INCLUDE and EXCLUDE appends to the filter.
  #
  # TODO: Perhaps FILTER should work on window types as well?

  FILTER ::= "&lt;filter&gt;"
               TAG?           # Introduced in version 2
               CLEAR?
               (INCLUDE | EXCLUDE)*
             "&lt;/filter&gt;"
             ;

  INCLUDE ::= "&lt;include&gt;"
                PATTERN
              "&lt;/include&gt;"
              ;

  EXCLUDE ::= "&lt;exclude&gt;"
                PATTERN
              "&lt;/exclude&gt;"
              ;

  CLEAR ::= "&lt;clear /&gt;" ;
  GET-ACTIVE-WINDOW ::= "&lt;get-active-window /&gt;" ;
  LIST-WINDOWS ::= "&lt;list-windows /&gt;" ;

  # The STRING can currently only have one value: "*", meaing all window-ids.
  # By default, the filter automatically includes windows that are opened from windows
  # in the INCLUDE filter. This can be prevented on a per-window basis by adding the
  # NOT-OPENED modifier.

  PATTERN ::= WINDOW-ID NOT-OPENED? | STRING ;

  NOT-OPENED ::= "&lt;not-opened /&gt;" ;

  ###
  # Events. Sent from host.
  #

  # ACTIVE-WINDOW is sent whenever the active window is changed, or
  # if the COMMAND GET-ACTIVE-WINDOW is sent. In the latter case, the
  # ID might be 0, meaning the active window is unknown.

  ACTIVE-WINDOW ::= "&lt;active-window&gt;"
                      WINDOW-ID
                    "&lt;/active-window&gt;"
                    ;

  #  WINDOW-LOADED is sent whenever a window has finished loading. This
  #  event happens after external resources are loaded. Note: if the page uses 
  #  scripting to dynamically change the page or load more data, this event may occur 
  #  multiple times for the same window. 

  WINDOW-LOADED ::= "&lt;window-loaded&gt;"
                      WINDOW-ID
                    "&lt;/window-loaded&gt;"
                    ;
					
  LIST-WINDOWS-REPLY ::= "&lt;list-windows-reply&gt;"
                           WINDOW*
                         "&lt;/list-windows-reply&gt;"
                         ;

  # This event will be sent every time there is a new window, or some window information changed.

  UPDATED-WINDOW ::= "&lt;updated-window&gt;"
                       WINDOW
                     "&lt;/updated-window&gt;"
                     ;

  WINDOW-CLOSED ::= "&lt;window-closed&gt;"
                      WINDOW-ID
                    "&lt;/window-closed&gt;"
                    ;

  # This event is sent as a reply to the FILTER command if the TAG was present.
  # Introduced in version 2:

  FILTER-REPLY ::= "&lt;filter-reply&gt;"
                     TAG
                     RESULT
                   "&lt;/filter-reply&gt;" ;

  ###
  # Other data types
  #

  # TODO: We probably want a lot more information about each window, like URL, shortcut
  #       icon, etc.

  WINDOW ::= "&lt;window&gt;"
               WINDOW-ID
               TITLE
               WINDOW-TYPE
               OPENER-ID
             "&lt;/window&gt;"
             ;

  # TODO: Some of these are obsolete, like "ad". Figure out which ones should really be present.

  WINDOW-TYPE ::= "&lt;window-type&gt;"
                  ( "normal"
                  | "download"
                  | "cache"
                  | "plugins"
                  | "history"
                  | "help"
                  | "mail_view"
                  | "mail_compose"
                  | "newsfeed_view"
                  | "ad"
                  | "im_view"
                  | "p2p_view"
                  | "brand_view"
                  | "print_selection"
                  | "js_console"
                  | "gadget"
                  | "controller"
                  | "info"
                  | "dialog"
                  | "thumbnail"
                  )
                  "&lt;/window-type&gt;"
                  ;

  # The OPENER-ID is the WINDOW-ID of the window who opened the window in question,
  # or 0 if it was not opened by another window.

  OPENER-ID ::= "&lt;opener-id&gt;"
                  UNSIGNED
                "&lt;/opener-id&gt;"
                ;

  TITLE ::= "&lt;title&gt;" TEXT "&lt;/title&gt;" ;

  # This is the result of an operation. So far it is only used in FILTER-REPLY.
  # Introduced in version 2:
  
  RESULT ::= "&lt;result&gt;"
             ( "ok"               # everything went ok
             | "failed"           # failed for unknown reason
             | "parsing failed"   # data not according to BNF
             | "not supported"    # data according to BNF, but used in an unsupported way
             )
             "&lt;/result&gt;" ;

  ###
  # Identifiers stolen from the ECMAScript debugger protocol:
  #

  WINDOW-ID ::= "&lt;window-id&gt;" UNSIGNED "&lt;/window-id&gt;" ;
  UNSIGNED ::= [0-9]+ ;
  STRING ::= "&lt;string&gt;" TEXT "&lt;/string&gt;" ;
  TEXT ::= BASE64-ENCODED-DATA | textual-data ;
  TAG ::= "&lt;tag&gt;" UNSIGNED "&lt;/tag&gt;" ;
  BASE64-ENCODED-DATA ::= "&lt;base64-encoded-data&gt;" textual-data "&lt;/base64-encoded-data&gt;" ;

</pre>

    <h2>

        Discussion</h2>

    <p>

        This protocol will prevent a lot of flooding from the other services, but it will on the other hand flood a bit itself.

        There is no way to stop it from sending <code>ACTIVE-WINDOW</code> messages for example.</p>

    <p>

        When Opera Dragonfly is used to debug on the desktop, it probably doesn't want to use this service at all, but just have

        the filters automatically set to include the active window. We might cater for this later. The way to do it with the current

        protocol would be to retrieve the active window and put that in the include filter manually. However the active window might

        have changed by the time Opera Dragonfly gets to do that. Additionally, we would like Opera Dragonfly to start with a certain

        DOM node selected. I see no other way than adding some C++ hooks to do this when debugging locally.</p>

</body>

</html>

