<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
          "http://www.w3.org/TR/html4/loose.dtd">
<html>

<head>
<link rel=stylesheet href="scope.css" />
<title>DOM inspector protocol</title>
<script src="markup_grammar.js"></script>
</head>

<body onload="markup_grammar()">
<h1>DOM inspector protocol</h1>

<pre id="grammar">
PROTOCOL ::= (COMMAND | EVENT)* ;
# Extend commands with the following

COMMAND ::= INSPECT-DOM ;

# Extend events with the following

EVENT ::= DOM-INSPECTION-REPLY ;

# The FORMAT option is for testing only, and will be removed in the final
# version (choosing either XML or JSON)

INSPECT-DOM ::=
  "&lt;inspect-dom>"
    TAG
    OBJECT-ID
    TRAVERSAL
    FORMAT
  "&lt;/inspect-dom>" ;

FORMAT ::=
  "&lt;format>"
    ( "xml" | "json" )
  "&lt;/format>" ;

# The traversal method can be any of the following:
#
# - "parent-node-chain-with-children" 
#     - take the parent node chain for the target node:
#     - add for each node in that chain all children, 
#       and for all children there first child,
#       if that is a text node and the only node, 
#       starting with the document node:
#
#         as pseudocode:
#
#           data
#           parentNodeChain
#           function getNodeData(node)
#             ...
#
#           function getView(node):
#             for child in node.childNodes:
#               data.add(getNodeData(child))
#               if child.childNodes.length == 1 and child.childNodes[0].nodeType == 3:
#                 data.add(getNodeData(child.childNodes[0]))
#               if child in parentNodeChain:
#                 getView(child)
#           data.add(getNodeData(parentNodeChain[0]))
#           getView(parentNodeChain[0])
#
# - "children"
#     - get node data for all children in their flow
#
# - "node"
#     - get node data for that node
#
# - "subtree"
#     - get node data for the whole document in the flow of it
#
#       as pseudocode:
#
#         data
#         function getNodeData(node)
#           ...
#
#         function getView(node):
#           for child in node.childNodes:
#             data.add(getNodeData(child))
#             if child.nodeType == 1:
#               getView(child)
#         data.add(getNodeData(documentNode))
#         getView(documentNode)

TRAVERSAL ::=
  "&lt;traversal>"
    ( "subtree" | "node" | "children" | "parent-node-chain-with-children" )
  "&lt;/traversal>" ;

DOM-INSPECTION-REPLY ::=
  "&lt;dom-inspection-reply>"
    TAG
    FLAT-DOM-DATA
  "&lt;/dom-inspection-reply>" ;

FLAT-DOM-DATA ::= NODE* ;

# NAMESPACE-PREFIX, ATTRIBUTES and CHILDREN-LENGTH only for type 1
# VALUE only for types 3, 4, 8
# PUBLIC-ID and SYSTEM-ID only for type 10
#
# Note to implementors changing from JSON:
#    Some names have changed from the JSON-protocol, namely dropping the -NODE
#    suffix and ATTRIBUTE- prefix, since it follows from the parent node.
#    Also, the order have changed to keep optional arguments grouped.

NODE ::=
  "&lt;node>"
    OBJECT-ID
    TYPE
    NAME
    DEPTH
    NAMESPACE-PREFIX?
    ATTRIBUTES?
    CHILDREN-LENGTH?
    VALUE?
    PUBLIC-ID?
    SYSTEM-ID?
  "&lt;/node>" ;

TYPE ::= "&lt;type>" UNSIGNED "&lt;/type>" ;
NAME ::= "&lt;name>" TEXT "&lt;/name>" ;
NAMESPACE-PREFIX ::= "&lt;namespace-prefix>" TEXT "&lt;/namespace-prefix>" ;
VALUE ::= "&lt;value>" TEXT "&lt;/value>" ;
DEPTH ::= "&lt;depth>" UNSIGNED "&lt;/depth>" ;
CHILDREN-LENGTH ::= "&lt;children-length>" UNSIGNED "&lt;/children-length>" ;
PUBLIC-ID ::= "&lt;public-id>" TEXT "&lt;/public-id>" ;
SYSTEM-ID ::= "&lt;system-id>" TEXT "&lt;/system-id>" ;
ATTRIBUTES ::= "&lt;attributes>" ATTRIBUTE* "&lt;/attributes>" ;

ATTRIBUTE ::=
  "&lt;attribute>"
    NAME-PREFIX?
    NAME
    VALUE
  "&lt;/attribute>" ;

NAME-PREFIX ::= "&lt;name-prefix>" TEXT "&lt;/name-prefix>" ;

###
# From ecmascript-debugger-protocol.html
#

TAG ::= "&lt;tag&gt;" UNSIGNED "&lt;/tag&gt;" ;
OBJECT-ID ::= "&lt;object-id&gt;" UNSIGNED "&lt;/object-id&gt;" ;
UNSIGNED ::= [0-9]+ ;
TEXT ::= BASE64-ENCODED-DATA | textual-data ;
BASE64-ENCODED-DATA ::= "&lt;base64-encoded-data&gt;" textual-data "&lt;/base64-encoded-data&gt;" ;
</pre>

</body>
</html>
