/* -*- Mode: c++; tab-width: 4; indent-tabs-mode: t; c-basic-offset: 4; c-file-style:"stroustrup" -*-
**
** Copyright (C) 1995-2010 Opera Software AS.  All rights reserved.
**
** This file is part of the Opera web browser.  It may not be distributed
** under any circumstances.
**
*/

group "unix.desktop_entry";

require _UNIX_DESKTOP_; 
require WIDGET_RUNTIME_SUPPORT; 

include "platforms/unix/base/common/desktop_entry.h";
include "adjunct/desktop_util/shortcuts/DesktopShortcutInfo.h";
include "modules/util/opfile/opfile.h";

global
{
	static void DeleteDir(const OpStringC& path)
	{
		OpFile dir;
		if (OpStatus::IsSuccess(dir.Construct(path))
					&& OpStatus::IsSuccess(dir.Delete(TRUE)))
		{
			return;
		}
		OP_ASSERT(!"Failed to delete temporary test directoy");
	}
}


test("DesktopEntry fails for unsupported locations")
{
	DesktopShortcutInfo shortcut_info;
	shortcut_info.m_destination = DesktopShortcutInfo::SC_DEST_QUICK_LAUNCH;
	DesktopEntry entry;
	verify_status(OpStatus::ERR_NOT_SUPPORTED, entry.Init(shortcut_info));
}

test("Create .desktop file in location pointed by XDG_DATA_HOME")
	file xdg_dir 			"data/xdg";
	file uni xdg_de_file	"data/xdg/applications/foobarbaz.desktop";
{
	const char* original_xdg_dir = getenv("XDG_DATA_HOME");
	OpString temp_path;
	verify_success(temp_path.Set(xdg_dir));

	setenv("XDG_DATA_HOME", xdg_dir, 1);

	DesktopShortcutInfo shortcut_info;
	shortcut_info.m_destination = DesktopShortcutInfo::SC_DEST_MENU;
	verify_success(shortcut_info.m_file_name.Set(UNI_L("foobarbaz")));

	DesktopEntry entry;
	verify_success(entry.Init(shortcut_info));
	verify_success(entry.Deploy());

	OpFile file;
	verify_success(file.Construct(xdg_de_file));
	BOOL file_exists = FALSE;
	verify_success(file.Exists(file_exists));
	verify(file_exists);

	verify_success(entry.Remove());

	verify_success(file.Exists(file_exists));
	verify(!file_exists);
}
finally
{
	if (NULL != original_xdg_dir)
	{
		setenv("XDG_DATA_HOME", original_xdg_dir, 1);
	}
	else
	{
		unsetenv("XDG_DATA_HOME");
	}
	DeleteDir(temp_path);
}
